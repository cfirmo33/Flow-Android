apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "com.yuyakaido.android.flow"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName "1.0.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

repositories {
    mavenCentral()
    maven { url 'http://dl.bintray.com/ngsw-taro/maven' }
    maven { url "https://jitpack.io" }
}

ext {
    supportLibVersion = '24.1.1'
    retrofitVersion = '2.1.0'
    okhttpVersion = '3.4.1'
    daggerVersion = '2.6'
}

dependencies {
    configurations.all {
        resolutionStrategy {
            force "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        }
    }

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Support Library
    compile "com.android.support:support-v4:$supportLibVersion"
    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"
    compile "com.android.support:customtabs:$supportLibVersion"

    // ReactiveX
    compile 'io.reactivex:rxjava:1.1.8'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.jakewharton.rxbinding:rxbinding-kotlin:0.4.0'

    // Image Loading
    compile 'com.github.bumptech.glide:glide:3.7.0'

    // Networking
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    compile ("com.squareup.retrofit2:converter-simplexml:$retrofitVersion") {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }
    compile "com.squareup.okhttp3:okhttp:$okhttpVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"

    // I/O
    compile 'com.squareup.okio:okio:1.9.0'

    // View
    compile 'com.roughike:bottom-bar:1.4.0.1'

    // Dependency Injection
    compile "com.google.dagger:dagger:$daggerVersion"
    apt "com.google.dagger:dagger-compiler:$daggerVersion"

    // Testing
    testCompile 'junit:junit:4.12'
    testCompile 'com.taroid.knit:knit:0.1.2'
    testCompile 'com.github.sys1yagi:kmockito:0.1.1'
    testCompile "com.squareup.okhttp3:mockwebserver:$okhttpVersion"
    testCompile 'org.robolectric:robolectric:3.1.2'
}
